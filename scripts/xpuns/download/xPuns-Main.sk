options:
	noperm: &cYou do not have permission to use this command
command /xpuns [<text>] [<text>]:
	aliases: xpuns:xpuns
	trigger:
		if arg-1 is not set:
			make player execute command "xpuns info"
		else if arg-1 is "info":
			message "&c&lxPuns&8 » &cInfo%nl%&8 » &7Version&8: &b2.0%nl%&8 » &7Created by&8: &b_iZ_"
			execute console command "tellraw %player% {""text"":""&8 » &bDownload"",""clickEvent"":{""action"":""open_url"",""value"":""https://minhaskamal.github.io/DownGit/##/home?url=https://github.com/izzzzthewiz/Iz-Devs/tree/main/scripts/xPuns""},""hoverEvent"":{""action"":""show_text"",""contents"":[""&bDownload link""]}}"
			execute console command "tellraw %player% {""text"":""&8 » &9Discord"",""clickEvent"":{""action"":""open_url"",""value"":""https://paypal.me/discord.io/iZzsLounge""},""hoverEvent"":{""action"":""show_text"",""contents"":[""&9Skript Discord server""]}}"
			execute console command "tellraw %player% {""text"":""&8 » &aDonate"",""clickEvent"":{""action"":""open_url"",""value"":""https://paypal.me/isaiahjw""},""hoverEvent"":{""action"":""show_text"",""contents"":[""&aDonation page""]}}"
		else if arg-1 is "reload":
			if player does not have permission "xpuns.reload":
				make player execute command "xpuns info"
				stop
			if arg-2 is not set:
				make player execute command "xpuns reload all"
			else if arg-2 is "config":
				make player execute command "sk reload %{xPuns::config::configFile}%"
			else if arg-2 is "main":
				make player execute command "sk reload %{xPuns::config::mainFile}%"
			else if arg-2 is "misc":
				make player execute command "sk reload %{xPuns::config::miscFile}%"
			else if arg-2 is "all":
				make player execute command "sk reload %{xPuns::config::configFile}%"
				make player execute command "sk reload %{xPuns::config::miscFile}%"
				make player execute command "sk reload %{xPuns::config::mainFile}%"
			else:
				message "&c&lxPuns&8 » &cUnknown file"
		else if arg-1 is "permissions" or "perms" or "nodes":
			if player does not have permission "xpuns.perms":
				make player execute command "xpuns info"
				stop
			message "&c&lxPuns&8 » &cPermissions"
			set {_p::*} to split "/xpuns info:N/A,/xpuns reload:xpuns.reload,/xpuns perms:xpuns.perms,/xpuns help:xpuns.help,/xpuns$reset:*,/punish:xpuns.punish,/history:xpuns.history,/history (player) clear:xpuns.history.clear,/warn:xpuns.warn,/kick:xpuns.kick,/mute:xpuns.mute,/ban:xpuns.ban,/ipban:xpuns.ipban,/alts:xpuns.alts,/ips:xpuns.ips,View silent punishments:xpuns.silent,Exempt:xpuns.exempt.(number)" at ","
			loop {_p::*}:
				set {_n::*} to loop-value split at ":"
				replace every "$" in {_n::1} with ":"
				message "&8 » &c%{_n::1}%&8: &b%{_n::2}%"
		else if arg-1 is "help":
			if player does not have permission "xpuns.help":
				make player execute command "xpuns info"
				stop
			message "&c&lxPuns&8 » &cHelp"
			set {_h::*} to split "/xpuns info:Displays info about this script,/xpuns reload:Reload an xPuns script,/xpuns perms:Displays a list of permissions,/xpuns help:Displays this message,/xpuns$reset:Resets all variables,/punish:Open the punishment menu,/history:Open the history menu,/warn:Warn a player,/kick:Kick a player,/mute:Mute or temp-mute a player,/ban:Ban or temp-ban a player,/ipban:IP-ban a player,/alts:View a player's alts,/ips:View a player's IP's" at ","
			loop {_h::*}:
				set {_n::*} to loop-value split at ":"
				replace every "$" in {_n::1} with ":"
				message "&8 » &c%{_n::1}%&8: &b%{_n::2}%"
		else:
			if player has permission "xpuns.help":
				make player execute command "xpuns help"
			else:
				make player execute command "xpuns info"
on inventory click:
	if name of event-inventory contains "Punish ":
		cancel event
		set {_p} to name of event-inventory
		replace every "Punish " in {_p} with ""
		if clicked slot is barrier:
			close player's inventory
		if clicked slot is oak sign:
			if {xPuns::silent::%uuid of player%} is not set:
				set {xPuns::silent::%uuid of player%} to " -s"
			else:
				delete {xPuns::silent::%uuid of player%}
			make player execute command "punish %{_p}%"
		if clicked slot is book:
			close player's inventory
			make player execute command "history %{_p}%"
		if clicked slot is lime dye:
			close player's inventory
			set {xPuns::chatarg::%player%} to "warnreason:%{_p}%"
			message "&c&lxPuns&8 » &7Type the reason you would like to warn &c%{_p}% &7for or &ccancel &7to cancel"
		if clicked slot is yellow dye:
			close player's inventory
			set {xPuns::chatarg::%player%} to "kickreason:%{_p}%"
			message "&c&lxPuns&8 » &7Type the reason you would like to kick &c%{_p}% &7for or &ccancel &7to cancel"
		if clicked slot is orange dye:
			close player's inventory
			set {xPuns::chatarg::%player%} to "mutedur:%{_p}%"
			message "&c&lxPuns&8 » &7Type the duration you would like to mute &c%{_p}% &7for or &ccancel &7to cancel"
		if clicked slot is red dye:
			close player's inventory
			set {xPuns::chatarg::%player%} to "bandur:%{_p}%"
			message "&c&lxPuns&8 » &7Type the duration you would like to ban &c%{_p}% &7for or &ccancel &7to cancel"
		if clicked slot is black dye:
			close player's inventory
			set {xPuns::chatarg::%player%} to "ipbanreason:%{_p}%"
			message "&c&lxPuns&8 » &7Type the reason you would like to IP-ban &c%{_p}% &7for or &ccancel &7to cancel"
	else if name of event-inventory contains "History - ":
		cancel event
		set {_p} to name of event-inventory
		replace every "History - " in {_p} with ""
		set {_p} to {_p} parsed as offline player
		set {_u} to uuid of {_p}
		if clicked slot is barrier:
			close player's inventory
		if clicked slot is paper:
			close player's inventory
			make player execute command "history %{_p}% clear"
		if click type is right mouse button:
			if clicked slot is lime dye or yellow dye:
				if player has permission "xpuns.history.clear":
					set {_lore::*} to lore of clicked slot
					set {_id} to uncolored {_lore::8}
					remove {_id} from {xPuns::received::%{_u}%::*}
					message "&c&lxPuns&8 » &a%{_id}% &awas removed from %{_p}%&a's punishment history"
					close player's inventory
					if size of {xPuns::received::%{_u}%::*} >= 1:
						make player execute command "history %{_p}%"
			if clicked slot is orange dye or red dye or black dye:
				if player has permission "xpuns.history.clear":
					set {_lore::*} to lore of clicked slot
					set {_id} to uncolored {_lore::10}
					remove {_id} from {xPuns::received::%{_u}%::*}
					message "&c&lxPuns&8 » &a%{_id}% &awas removed from %{_p}%&a's punishment history"
					close player's inventory
					if size of {xPuns::received::%{_u}%::*} >= 1:
						make player execute command "history %{_p}%"
				
command /punish <offline player> [<text>]:
	permission: xpuns.punish
	permission message: &c&lxPuns&8 » {@noperm}
	usage: &c&lxPuns&8» &cUsage: /punish (player)
	aliases: xpuns:punish, pun, xpuns:pun
	trigger:
		open chest inventory with 4 rows named "Punish %arg-1%" to player
		xPunsFormatRows(player, 4)
		set slot 30 of player's current inventory to barrier named "&cClose"
		xPunsFormatSilent(player, 32)
		set slot 4 of player's current inventory to book named "&bHistory" with lore "&7Click to view history"
		set slot 12 of player's current inventory to lime dye named "&aWarn" with lore "&7Click to warn"
		set slot 14 of player's current inventory to yellow dye named "&eKick" with lore "&7Click to kick"
		set slot 20 of player's current inventory to orange dye named "&6Mute" with lore "&7Click to mute"
		set slot 22 of player's current inventory to red dye named "&cBan" with lore "&7Click to ban"
		set slot 24 of player's current inventory to black dye named "&4IP-Ban" with lore "&7Click to IP-ban"
command /warn <offline player> [<text>]:
	permission: xpuns.warn
	permission message: &c&lxPuns&8 » {@noperm}
	usage: &c&lxPuns&8 » &cUsage: /warn (player) (reason) (-n) (-s)
	aliases: xpuns:warn
	trigger:
		if xPunsCheckPerm(sender, arg-1) is false:
			message "&c&lxPuns&8 » &cYou cannot punish %arg-1%"
			stop
		set {_id} to "warn-%floor(unix timestamp of now)%"
		set {_r} to "No reason given"
		set {_n} to "N/A"
		set {_sl} to false
		if arg-2 is set:
			set {_r} to xPunsGetReason(arg-2)
			set {_n} to xPunsGetNote(arg-2)
			set {_sl} to xPunsCheckForSilent(arg-2)
		xPunsMessage("warn", arg-1, "%sender%", {_r}, "PERMANENT", {_n}, {_sl})
		xPunsAddPunishment(sender, arg-1, "Warn", {_r}, {_n}, "Permanent", {_sl}, {_id})
		send "&c&lxPuns&8 » &cYou have been warned!%nl%&8 » &7Reason&8: &6%{_r}%%nl%&8 » &7Issued by&8: &b%sender%" to arg-1
		if {_n} is not "N/A":
			send "&c%{_n}%" to arg-1
command /kick <offline player> [<text>]:
	permission: xpuns.kick
	permission message: &c&lxPuns&8 » {@noperm}
	usage: &c&lxPuns&8 » &cUsage: /kick (player) (reason) (-n) (-s)
	aliases: xpuns:kick
	trigger:
		set {_p::*} to all players
		if {_p::*} does not contain arg-1:
			message "&c&lxPuns&8 » &c%arg-1% &cis offline"
			stop
		if xPunsCheckPerm(sender, arg-1) is false:
			message "&c&lxPuns&8 » &cYou cannot punish %arg-1%"
			stop
		set {_id} to "kick-%floor(unix timestamp of now)%"
		set {_r} to "No reason given"
		set {_n} to "N/A"
		set {_sl} to false
		if arg-2 is set:
			set {_r} to xPunsGetReason(arg-2)
			set {_n} to xPunsGetNote(arg-2)
			set {_sl} to xPunsCheckForSilent(arg-2)
		if {_n} is not "N/A":
			kick arg-1 because "&7%nl%&c&lxPuns&8 » &cYou have been kicked!%nl%&8 » &7Reason&8: &6%{_r}%%nl%&8 » &7Issued by&8: &b%sender%%nl%&c%{_n}%"
		else:
			kick arg-1 because "&7%nl%&c&lxPuns&8 » &cYou have been kicked!%nl%&8 » &7Reason&8: &6%{_r}%%nl%&8 » &7Issued by&8: &b%sender%"
		xPunsMessage("kick", arg-1, "%sender%", {_r}, "PERMANENT", {_n}, {_sl})
		xPunsAddPunishment(sender, arg-1, "Kick", {_r}, {_n}, "Permanent", {_sl}, {_id})
command /mute <offline player> [<text>]:
	permission: xpuns.mute
	permission message: &c&lxPuns&8 » {@noperm}
	usage: &c&lxPuns&8 » &cUsage: /mute (player) (duration) (reason) (-n) (-s)
	aliases: xpuns:mute, tempmute, xpuns:tempmute
	trigger:
		if xPunsCheckPerm(sender, arg-1) is false:
			message "&c&lxPuns&8 » &cYou cannot punish %arg-1%"
			stop
		if xPunsCheckMute(arg-1) is true:
			message "&c&lxPuns&8 » &c%arg-1% &cis already muted"
			stop
		set {_id} to "mute-%floor(unix timestamp of now)%"
		set {_r} to "No reason given"
		set {_n} to "N/A"
		set {_d} to "Permanent"
		set {_sl} to false
		if arg-2 is set:
			set {_r} to xPunsGetReason(arg-2)
			set {_n} to xPunsGetNote(arg-2)
			set {_d} to xPunsGetDuration(arg-2)
			set {_sl} to xPunsCheckForSilent(arg-2)
		xPunsMessage("mute", arg-1, "%sender%", {_r}, xPunsGetUpperTimespan({_d}), {_n}, {_sl})
		xPunsAddPunishment(sender, arg-1, "Mute", {_r}, {_n}, {_d}, {_sl}, {_id})
		send "&c&lxPuns&8 » &cYou have been muted!%nl%&8 » &7Reason&8: &6%{_r}%%nl%&8 » &7Duration&8: &c%xPunsGetTimespan({_d})%%nl%&8 » &7Issued by&8: &b%sender%" to arg-1
		if {_n} is not "N/A":
			send "&c%{_n}%" to arg-1
		set {xPuns::muted::%uuid of arg-1%} to {_id}
command /ban <offline player> [<text>]:
	permission: xpuns.ban
	permission message: &c&lxPuns&8 » {@noperm}
	usage: &c&lxPuns&8 » &cUsage: /ban (player) (duration) (reason) (-n) (-s)
	aliases: xpuns:ban, tempban, xpuns:tempban
	trigger:
		if xPunsCheckPerm(sender, arg-1) is false:
			message "&c&lxPuns&8 » &cYou cannot punish %arg-1%"
			stop
		if xPunsCheckBan(arg-1) is true:
			message "&c&lxPuns&8 » &c%arg-1% &cis already banned"
			stop
		set {_id} to "ban-%floor(unix timestamp of now)%"
		set {_r} to "No reason given"
		set {_n} to "N/A"
		set {_d} to "Permanent"
		set {_sl} to false
		if arg-2 is set:
			set {_r} to xPunsGetReason(arg-2)
			set {_n} to xPunsGetNote(arg-2)
			set {_d} to xPunsGetDuration(arg-2)
			set {_sl} to xPunsCheckForSilent(arg-2)
		xPunsMessage("ban", arg-1, "%sender%", {_r}, xPunsGetUpperTimespan({_d}), {_n}, {_sl})
		xPunsAddPunishment(sender, arg-1, "Ban", {_r}, {_n}, {_d}, {_sl}, {_id})
		if {_n} is not "N/A":
			kick arg-1 because "&7%nl%&c&lxPuns&8 » &cYou have been banned!%nl%&8 » &7Reason&8: &6%{_r}%%nl%&8 » &7Duration&8: &c%xPunsGetTimespan({_d})%%nl%&8 » &7Issued by&8: &b%sender%%nl%&c%{_n}%"
		else:
			kick arg-1 because "&7%nl%&c&lxPuns&8 » &cYou have been banned!%nl%&8 » &7Reason&8: &6%{_r}%%nl%&8 » &7Duration&8: &c%xPunsGetTimespan({_d})%%nl%&8 » &7Issued by&8: &b%sender%"
		set {xPuns::banned::%uuid of arg-1%} to {_id}
command /ipban <text> [<text>]:
	permission: xpuns.ipban
	permission message: &c&lxPuns&8 » {@noperm}
	usage: &c&lxPuns&8 » &cUsage: /ipban (player | IP) (reason) (-n) (-s)
	aliases: xpuns:ipban, ip-ban, xpuns:ip-ban, banip, xpuns:banip, ban-ip, xpuns:ban-ip
	trigger:
		set {_id} to "ipban-%floor(unix timestamp of now)%"
		set {_r} to "No reason given"
		set {_n} to "N/A"
		set {_d} to "Permanent"
		set {_sl} to false
		if arg-2 is set:
			set {_r} to xPunsGetReason(arg-2)
			set {_n} to xPunsGetNote(arg-2)
			set {_sl} to xPunsCheckForSilent(arg-2)
		set {_ip::*} to arg-1 split at "."
		set {_fromip} to true
		if size of {_ip::*} is 4:
			loop {_ip::*}:
				set {_n} to loop-value parsed as integer
				if {_n} is not an integer:
					set {_fromip} to false
					stop loop
			if {_fromip} is false:
				message "&c&lxPuns&8 » &cInvalid IP"
				stop
			if {_fromip} is true:
				set {_u::*} to xPunsFetchAccountsFromIP(arg-1)
				if size of {_u::*} < 1:
					message "&c&lxPuns&8 » &cNo accounts are linked to this IP"
					stop
				loop {_u::*}:
					set {_p} to loop-value parsed as offline player
					if xPunsCheckPerm(sender, {_p}) is false:
						message "&c&lxPuns&8 » &cYou cannot punish %{_p}%"
						stop
				loop {_u::*}:
					set {_p} to loop-value parsed as offline player
					set {xPuns::banned::%loop-value%} to {_id}
					xPunsAddPunishment(sender, {_p}, "IP-Ban", {_r}, {_n}, "Permanent", {_sl}, {_id})
					if {_n} is not "N/A":
						kick {_p} because "&7%nl%&c&lxPuns&8 » &cYou have been IP-banned!%nl%&8 » &7Reason&8: &6%{_r}%%nl%&8 » &7Duration&8: &c%xPunsGetTimespan({_d})%%nl%&8 » &7Issued by&8: &b%sender%%nl%&c%{_n}%"
					else:
						kick {_p} because "&7%nl%&c&lxPuns&8 » &cYou have been IP-banned!%nl%&8 » &7Reason&8: &6%{_r}%%nl%&8 » &7Duration&8: &c%xPunsGetTimespan({_d})%%nl%&8 » &7Issued by&8: &b%sender%"
				xPunsMessageIP("ipbanIP", arg-1, "%sender%", {_r}, "PERMANENT", {_sl})
				stop
		set {_p} to arg-1 parsed as offline player
		if xPunsCheckPerm(sender, {_p}) is false:
			message "&c&lxPuns&8 » &cYou cannot punish %{_p}%"
			stop
		set {_u::*} to xPunsFetchAccountsFromPlayer({_p})
		if size of {_u::*} < 1:
			message "&c&lxPuns&8 » &cNo IP's are linked to this account"
			stop
		xPunsMessage("ipban", {_p}, "%sender%", {_r}, "PERMANENT", {_n}, {_sl})
		loop {_u::*}:
			set {_p} to loop-value parsed as offline player
			xPunsAddPunishment(sender, {_p}, "IP-Ban", {_r}, {_n}, "Permanent", {_sl}, {_id})
			set {xPuns::banned::%loop-value%} to {_id}
			set {_p} to loop-value parsed as offline player
			if {_n} is not "N/A":
				kick {_p} because "&7%nl%&c&lxPuns&8 » &cYou have been IP-banned!%nl%&8 » &7Reason&8: &6%{_r}%%nl%&8 » &7Duration&8: &c%xPunsGetTimespan({_d})%%nl%&8 » &7Issued by&8: &b%sender%%nl%&c%{_n}%"
			else:
				kick {_p} because "&7%nl%&c&lxPuns&8 » &cYou have been IP-banned!%nl%&8 » &7Reason&8: &6%{_r}%%nl%&8 » &7Duration&8: &c%xPunsGetTimespan({_d})%%nl%&8 » &7Issued by&8: &b%sender%"
command /unmute <offline player> [<text>]:
	permission: xpuns.unmute
	permission message: &c&lxPuns&8 » {@noperm}
	usage: &c&lxPuns&8 » &cUsage: /unmute (player) (-s)
	aliases: xpuns:unmute
	trigger:
		if xPunsCheckMute(arg-1) is false:
			message "&c&lxPuns&8 » &c%arg-1% &cis not muted"
			stop
		if arg-2 contains "-s":
			xPunsMessage("unmute", arg-1, "%sender%", "N/A", "PERMANENT", "N/A", true)
		else:
			xPunsMessage("unmute", arg-1, "%sender%", "N/A", "PERMANENT", "N/A", false)
		clear {xPuns::muted::%uuid of arg-1%}
command /unban <offline player> [<text>]:
	permission: xpuns.unban
	permission message: &c&lxPuns&8 » {@noperm}
	usage: &c&lxPuns&8 » &cUsage: /unban (player) (-s)
	aliases: xpuns:unban
	trigger:
		if xPunsCheckBan(arg-1) is false:
			message "&c&lxPuns&8 » &c%arg-1% &cis not banned"
			stop
		if arg-2 contains "-s":
			xPunsMessage("unban", arg-1, "%sender%", "N/A", "PERMANENT", "N/A", true)
		else:
			xPunsMessage("unban", arg-1, "%sender%", "N/A", "PERMANENT", "N/A", false)
		clear {xPuns::banned::%uuid of arg-1%}
command /unipban <text> [<text>]:
	permission: xpuns.unmute
	permission message: &c&lxPuns&8 » {@noperm}
	usage: &c&lxPuns&8 » &cUsage: /unbanip (IP) (-s)
	aliases: xpuns:unipban, unbanip, xpuns:unbanip
	trigger:
		set {_ip::*} to arg-1 split at "."
		if size of {_ip::*} is not 4:
			message "&c&lxPuns&8 » &cInvalid IP"
			stop
		loop {_ip::*}:
			set {_n} to loop-value parsed as integer
			if {_n} is not an integer:
				message "&c&lxPuns&8 » &cInvalid IP"
				stop
		if xPunsCheckIPbanFromIP(arg-1) is false:
			message "&c&lxPuns&8 » &cIP %arg-1% &cis not banned"
			stop
		if arg-2 contains "-s":
			xPunsMessageIP("unipban", arg-1, "%sender%", "N/A", "PERMANENT", true)
		else:
			xPunsMessageIP("unipban", arg-1, "%sender%", "N/A", "PERMANENT", false)
		set {_u::*} to xPunsFetchAccountsFromIP(arg-1)
		loop {_u::*}:
			delete {xPuns::banned::%loop-value%}
command /alts <offline player>:
	permission: xpuns.alts
	permission message: &c&lxPuns&8 » {@noperm}
	usage: &c&lxPuns&8 » &cUsage: /alts (player)
	aliases: xpuns:alts
	trigger:
		message "&c&lxPuns&8 » &cAlts &8(&b%arg-1%&8)"
		set {_p::*} to all players
		set {_u::*} to xPunsFetchAccountsFromPlayer(arg-1)
		if size of {_u::*} > 1:
			loop {_u::*}:
				set {_p} to loop-value parsed as offline player
				if xPunsCheckBan({_p}) is true:
					message "&8 » &c%{_p}%"
				else if xPunsCheckMute({_p}) is true:
					message "&8 » &6%{_p}%"
				else if {_p::*} contains {_p}:
					message "&8 » &a%{_p}%"
				else:
					message "&8 » &7%{_p}%"
			stop
		if xPunsCheckBan(arg-1) is true:
			message "&8 » &c%arg-1%"
		else if xPunsCheckMute(arg-1) is true:
			message "&8 » &6%arg-1%"
		else if {_p::*} contains arg-1:
			message "&8 » &a%arg-1%"
		else:
			message "&8 » &7%arg-1%"
command /ips <offline player>:
	permission: xpuns.ips
	permission message: &c&lxPuns&8 » {@noperm}
	usage: &c&lxPuns&8 » &cUsage: /ips (player)
	aliases: xpuns:ips
	trigger:
		set {_ips::*} to xPunsFetchIPs(arg-1)
		if size of {_ips::*} >= 1:
			message "&c&lxPuns&8 » &cIP's &8(&b%arg-1%&8)"
			loop {_ips::*}:
				execute console command "tellraw %player% {""text"":""&8 » &b%loop-value%"",""clickEvent"":{""action"":""suggest_command"",""value"":""%loop-value%""},""hoverEvent"":{""action"":""show_text"",""contents"":[""&aClick to copy""]}}"
			stop
		message "&cxPuns&8 » &c%arg-1% &chas no known IP's"
command /history <offline player> [<text>]:
	permission: xpuns.history
	permission message: &c&lxPuns&8 » {@noperm}
	usage: &c&lxPuns&8 » &cUsage: /history (player) (clear)
	aliases: xpuns:history, puns, xpuns:puns
	trigger:
		if arg-2 is "clear":
			if player has permission "xpuns.history.clear":
				clear {xPuns::received::%uuid of arg-1%::*}
				message "&c&lxPuns&8 » &a%arg-1%&a's punishment history has been cleared"
				stop
			message "&c&lxPuns&8 » &cYou do not have permission to clear history"
		if size of {xPuns::received::%uuid of arg-1%::*} < 1:
			message "&c&lxPuns&8 » &c%arg-1% &chas no punishment history"
			stop
		set {_rows} to xPunsGetRows(size of {xPuns::received::%uuid of arg-1%::*})
		open chest inventory with {_rows} rows named "History - %arg-1%" to player
		xPunsFormatRows(player, {_rows})
		if player has permission "xpuns.history.clear":
			set slot {_rows}*9-6 of player's current inventory to barrier named "&cClose"
			set slot {_rows}*9-4 of player's current inventory to paper named "&6Clear History"
		else:
			set slot {_rows}*9-5 of player's current inventory to barrier named "&cClose"
		set {_s} to -1
		loop {xPuns::received::%uuid of arg-1%::*}:
			add 1 to {_s}
			xPunsFormatHistory(player, {_s}, loop-value)
command /xpuns:reset [<text>]:
	permission: *
	permission message: &c&lxPuns&8 » {@noperm}
	trigger:
		if {xPuns::reset::%sender%} is not set:
			set {xPuns::reset::%sender%} to now
			message "&c&lxPuns&8 » &cWARNING: This command resets all variables. It should only be used in an emergency. If you wish to proceed, type the command again within 10 seconds."
			stop
		if difference between {xPuns::reset::%sender%} and now > 10 seconds:
			set {xPuns::reset::%sender%} to now
			message "&c&lxPuns&8 » &cWARNING: This command resets all variables. It should only be used in an emergency. If you wish to proceed, type the command again within 10 seconds."
			stop
		clear {xPuns::*}
		message "&c&lxPuns&8 » &aAll variables were reset"
		message "&c&lxPuns&8 » &cWARNING: You must manually reload the xPuns config file for the script to work properly!"
